# yacker grammar for jsg files
# github URL: https://github.com/ericprud/jsg/blob/master/doc/jsg.bnf
# yacker URL: https://www.w3.org/2005/01/yacker/uploads/jsg?lang=perl

doc              : directive* grammarDef
directive        : ".TYPE" ID ";"
		 | ".IGNORE" ID* ";"
grammarDef       : (objectDef | arrayDef | nonObject | terminal)*
objectDef        : ID objectExpr
objectExpr       : '{'  '}'
                 | '{' particle+ ('|' particle*)* '}'
                 | '{' ID '->' propertyType '}'
arrayDef         : ID arrayExpr
arrayExpr        : '[' propertyType ('|' propertyType)* ']'
particle         : ID cardinality? ','?
                 | propertyOrGroup ','?
propertyOrGroup  : ID ':' propertyType cardinality?
                 | '(' ID ('|' ID)+ ')' ':' propertyType cardinality?
                 | '(' propertyOrGroup+ ('|' propertyOrGroup+)+ ')'
propertyType     : ID
                 | STRING
                 | objectExpr
                 | arrayExpr
                 | '(' typeAlternatives ')'
		 | '.'
typeAlternatives : (ID|STRING) ('|' (ID|STRING))+
nonObject        : ID '=' particle+ ('|' particle*)* ';'
terminal         : ID ':' STRING ('+' STRING)*
cardinality      : '?' | '*'

@terminals

ID               : [a-zA-Z_]+
STRING           : '"' [^"]+ '"'
@pass            : [ \t\r\n]+
                 | "#" [^\r\n]*
